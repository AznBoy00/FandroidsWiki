apply plugin: 'com.facebook.testing.screenshot'

def grantPermissionTask(variant, String taskName) {
    task (taskName, type: Exec, dependsOn: variant.install) {
        String adb = android.getAdbExe().toString()
        String pkg = variant.applicationId
        String readPermission = 'android.permission.READ_EXTERNAL_STORAGE'
        String writePermission = 'android.permission.WRITE_EXTERNAL_STORAGE'
        String cmd = "pm grant ${pkg} ${readPermission} && pm grant ${pkg} ${writePermission}"
        commandLine adb, 'shell', cmd
    }
    variant.testVariant.connectedInstrumentTest.dependsOn += taskName
}

android.applicationVariants.all { variant ->
    if (variant.testVariant) {
        grantPermissionTask variant, "grantPermission${variant.name.capitalize()}"
    }
}

String variantName = android.testBuildType == 'debug' ? 'DevDebug' : 'AlphaRelease'
task("screenshotTests${variantName}", dependsOn: ['recordMode', 'screenshotTests']) {
    screenshots {
        testApkTarget = "package${variantName}AndroidTest"
        connectedAndroidTestTarget = "connected${variantName}AndroidTest"
    }
}

tasks.getByName("pullScreenshots").dependsOn 'recordMode'